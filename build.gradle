apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	failOnError = true
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- DEPENDENCIES ---

dependencies {

    testImplementation 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

	// ================== DATATREE CORE =======================
	    
    // https://mvnrepository.com/artifact/com.github.berkesa/datatree-core
	implementation group: 'com.github.berkesa', name: 'datatree-core', version: '1.1.2'
	
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// version = '1.0.9-SNAPSHOT'
version = '1.0.9'

modifyPom {
    project {
        artifactId 'datatree-promise'
        name 'Promise for Java'
        description "ES6-like Promise, based on the Java8's CompletableFuture API."
        url 'https://berkesa.github.io/datatree-promise/'
        inceptionYear '2018'

        scm {
            url 'https://berkesa.github.io/datatree-promise/'
            connection 'scm:https://github.com/berkesa/datatree-promise.git'
            developerConnection 'scm:git://github.com/berkesa/datatree-promise.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-core'
                version '1.1.2'
                scope 'runtime'
            }        
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}